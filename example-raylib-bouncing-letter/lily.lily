

ansi-clear-screen
    "\u{1B}c"

type state =
    { window-width dec
    , window-height dec
    , x dec
    , y dec
    , velocity-x dec
    , velocity-y dec
    }

initial-state \{ window-width :dec:window-width, window-height :dec:window-height } >
    :state:
    { x 0.0
    , y 0.0
    , velocity-x 0.0
    , velocity-y 0.4
    , window-width window-width
    , window-height window-height
    }

interface \:state:state >
    state
    | { window-width :dec:window-width
      , window-height :dec:window-height
      , x :dec:x
      , y :dec:y
      , velocity-x :dec:velocity-x
      , velocity-y :dec:velocity-y
      }
    >
    [ :io state:Display
        [ = explainer-text
            "arrow keys"
          = explainer-text-height
            0.1
          { str explainer-text
          , x
                dec-mul window-width
                    (dec-add 0.5
                        (dec-mul -0.5 (text-width explainer-text explainer-text-height))
                    )
          , y dec-mul window-height 0.2
          , size dec-mul window-height explainer-text-height
          }
        , { str "Ã–"
          , x dec-mul window-width (dec-add 0.5 (dec-add x (dec-mul -0.5 player-width)))
          , y dec-mul window-height (dec-add 0.5 (dec-negate (dec-add y (dec-mul 0.5 player-width))))
          , size dec-mul window-height player-height
          }
        ]
    , :io state:Key-pressed
        \:unt:maybe-pressed-key >
            maybe-pressed-key
            | # right arrow
              262
            >
                { ..state, velocity-x dec-add x 0.8 }
            | # left arrow
              263
            >
                { ..state, velocity-x dec-add x -0.8 }
            | :unt:_ >
                state
    , :io state:Frame-passed
        \:dec:seconds-since-last-frame >
            = new-velocity-y
                dec-add velocity-y (dec-mul -0.8 seconds-since-last-frame)
            = future-x
                dec-add x (dec-mul velocity-x seconds-since-last-frame)
            = future-y
                dec-add y (dec-mul new-velocity-y seconds-since-last-frame)
            { ..state
            , velocity-y
                dec-order future-y (dec-add 0.5 (dec-mul -0.5 player-height))
                | :order:Greater > dec-negate (dec-absolute new-velocity-y)
                | :order:_ >
                dec-order future-y (dec-add -0.5 (dec-mul 0.5 player-height))
                | :order:Less > dec-absolute new-velocity-y
                | :order:_ >
                new-velocity-y
            , velocity-x
                dec-order future-x (dec-add 0.5 (dec-mul -0.5 player-width))
                | :order:Greater > dec-negate (dec-absolute velocity-x)
                | :order:_ >
                dec-order future-x (dec-add -0.5 (dec-mul 0.5 player-width))
                | :order:Less > dec-absolute velocity-x
                | :order:_ >
                velocity-x
            , x future-x
            , y future-y
            }
    ]

player-height
    0.2

player-width
    text-height-to-width player-height

text-height-to-width \:dec:text-height >
    dec-mul text-height 0.5

text-width \:str:text, :dec:text-height >
    dec-mul (text-height-to-width text-height)
        (unt-to-dec (str-byte-count text))

choice io Future
    | Display vec { str str, x dec, y dec, size dec }
    | Key-pressed \unt > Future
    | Frame-passed \dec > Future

